buildscript {
    repositories {
        jcenter()
        maven { url 'https://www.jitpack.io' }
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            name="onkiup"
            url = "https://raw.github.com/chedim/maven-repo/master/";
        }
        maven {
            name = 'SpongePowered'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.onkiup.gradle:minedroid:1.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'minedroid'
apply plugin: 'org.spongepowered.mixin'

//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
repositories {
    maven {
        name="onkiup"
        url = "https://raw.github.com/chedim/maven-repo/master/";
    }
    maven { url "https://jitpack.io" }
    maven {
        name = 'spongepowered-repo'
        url = 'http://repo.spongepowered.org/maven/'
    }
}

version = "0.1"
group = "fr.undev.transware" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "transware"
// https://mvnrepository.com/artifact/org.ini4j/ini4j



sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    coreMod = 'fr.undev.transware.mixin.MixinLoaderForge'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
    compile("org.spongepowered:mixin:0.7.11-SNAPSHOT") { // do NOT update this to 8.0 or above, it breaks compatibility with future client
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core' // we want to exclude this as well because 0.7.11 includes it too new for MC
    }
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'org.ini4j', name: 'ini4j', version: '0.5.1'
    compile group: 'com.github.Vatuu', name: 'discord-rpc', version: '1.6.2'
    compile group: 'org.graylog.repackaged', name: 'os-platform-finder', version: '1.2.3'
    compile group: 'com.github.oshi', name: 'oshi-core', version: '5.1.2'
    compile 'com.onkiup.minedroid:minedroid:1.3.2'

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

    jar {
        manifest {
            attributes(
                    'MixinConfigs': 'mixins.transware.json',
                    'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                    'TweakOrder': 0,
                    'FMLCorePluginContainsFMLMod': 'true',
                    'FMLCorePlugin': 'fr.undev.transware.mixin.MixinLoaderForge',
                    'ForceLoadAsMod': 'true',
                    'FMLAT': 'trans_at.cfg'
            )
        }
    }

shadowJar {
    dependencies {
        include(dependency('com.googlecode.json-simple:json-simple'))
        include(dependency('org.ini4j:ini4j'))
        include(dependency('com.github.Vatuu:discord-rpc'))
        include(dependency('org.graylog.repackaged:os-platform-finder'))
        include(dependency('com.github.oshi:oshi-core'))
        include(dependency('org.spongepowered:mixin'))
    }
    exclude 'dummyThing' // edit me
    classifier = 'release'
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.transware.refmap.json'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)
